import copy
import itertools
import numpy as np
cimport numpy as cnp

cnp.import_array()

ctypedef cnp.int64_t DTYPE_t

cpdef tuple get_parallelotope_points_np(
        cnp.ndarray[DTYPE_t, ndim=1] snf,
        DTYPE_t det,
        cnp.ndarray[DTYPE_t, ndim=2] VDinv,
        cnp.ndarray[DTYPE_t, ndim=2] R,
        int height=-1,
    ):
    """
    Get all the integer points in the half-open parallelotope generated by
    the columns of R. This is a modification of the SageMath implementation
    at sage/src/sage/geometry/integral_points.pxi (GPLv2+).
    Description of the parameters:
    snf: the diagonal Smith normal form of the matrix R
    det: the determinant of R
    VDinv: the inverse of the matrix VD, where D is the diagonal matrix
        in the Smith normal form of R
    R: the matrix whose columns generate the parallelotope
    height: if given, only return points with the given "height" (= first coordinate)
    """
    cdef int i, j
    cdef int dim = VDinv.shape[0]
    cdef int ambient_dim = R.shape[0]
    cdef DTYPE_t s = 0
    cdef list gens = []
    cdef cnp.ndarray[DTYPE_t, ndim=1] gen = np.zeros(ambient_dim, dtype=np.int64)
    cdef cnp.ndarray[DTYPE_t, ndim=1] q_times_d = np.zeros(dim, dtype=np.int64)
    for base in itertools.product(*[range(i) for i in snf]):
        for i in range(dim):
            s = 0
            for j in range(dim):
                s += VDinv[i, j] * base[j]
            q_times_d[i] = s % det
        for i in range(ambient_dim):
            s = 0
            for j in range(dim):
                s += R[i, j] * q_times_d[j]
            gen[i] = s / det
            if i == 0 and height >= 0 and gen[0] != height:
                continue
        else:
            # only executed if the loop did not break
            gens.append(copy.copy(gen))
    return tuple(gens)
